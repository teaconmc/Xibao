plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

group = 'org.teacon'
version = '3.0.0'

java {
    archivesBaseName = 'Xibao-Forge-1.20'
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: 'official', version: '1.20'

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'
            mods {
                xibao { source sourceSets.main }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'
            mods {
                xibao { source sourceSets.main }
            }
        }

        // Yes this mod does not have any data to generate.
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories { /* no repository here*/ }

dependencies {
    minecraft 'net.minecraftforge:forge:1.20-46.0.1'
}

jar {
    manifest {
        attributes([
                "Specification-Title": "Xibao",
                "Specification-Vendor": "IzzelAliz",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": "${project.version}",
                "Implementation-Vendor": "TeaConMC",
                "Implementation-Timestamp": "${Instant.now().truncatedTo(java.time.temporal.ChronoUnit.SECONDS)}"
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        release(MavenPublication) {
            groupId = "org.teacon"
            artifactId = "Xibao-Forge-1.20"

            artifact jar
            pom {
                name = 'Xibao for Minecraft 1.20'
                description = 'Ask IzzelAliz what is this'
                licenses {
                    license {
                        name = 'BSD-3-Clause'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "teacon"
            url = "s3://maven/"
            credentials(AwsCredentials) {
                accessKey = System.env.ARCHIVE_ACCESS_KEY
                secretKey = System.env.ARCHIVE_SECRET_KEY
            }
        }
    }
}

tasks.withType(PublishToMavenRepository) {
    onlyIf { System.env.ARCHIVE_ACCESS_KEY && System.env.ARCHIVE_SECRET_KEY }
}

/**
 * A simple task to pass down the artifact name and path to other GitHub actions.
 */
task("githubActionOutput") {
    onlyIf { System.env.GITHUB_ACTIONS }
    doLast {
        println "::set-output name=artifact_path::${jar.archiveFile.get().asFile.absolutePath}"
        println "::set-output name=artifact_name::${jar.archiveFileName.get()}"
    }
}
